@ECHO OFF
SETLOCAL ENABLEDELAYEDEXPANSION

SET "BN_RENDERSAMPLES=0"

SET "BN_VGMPATH=%~1"
SET /A "BN_PREGOFFSET=(%~2 - 1) * 4"
SET /A "BN_REG0=BN_PREGOFFSET + 0"
REM SET /A "BN_REG1=BN_PREGOFFSET + 1"
SET /A "BN_REG2=BN_PREGOFFSET + 2"
SET /A "BN_REG3=BN_PREGOFFSET + 3"
SET "BN_POINTER=%~3"

SET "BN_WAVE_P0=128"
SET "BN_WAVE_P1=192"
SET "BN_WAVE_P2=240"
SET "BN_WAVE_P3=252"

SET "BN_CHSTAT_PULSE_DUNTIL=1"
SET "BN_CHSTAT_PULSE_PHASE=0"

SET "BN_CHREG_PULSE_DUTY=0"
SET "BN_CHREG_PULSE_VOL=0"
SET "BN_CHREG_PULSE_ENVMODE=1"
set "BN_CHREG_PULSE_CV=1"
SET "BN_CHREG_PULSE_PL=0"
SET "BN_CHREG_PULSE_PH=0"
REM SET "BN_CHREG_PULSE_LEN=0"
REM TODO : LENGTH COUNTER, SWEEP UNITS

REM TODO : DETECT JUMP / EOF POINT
:A
SET "BN_RENDERSAMPLES=0"
FOR /F "TOKENS=1 USEBACKQ" %%A IN (`BINREADWRITE.EXE "!BN_VGMPATH!" READ !BN_POINTER! 1`) DO SET "BN_COMMAND=%%A"
SET /A "BN_POINTER+=1"
SET /A "BN_COMMAND_HIGH_4_BITS=BN_COMMAND >> 4"
SET /A "BN_COMMAND_LOW_4_BITS=BN_COMMAND & 0x0F"

IF !BN_COMMAND! EQU 180 (
	FOR /F "TOKENS=1,2 USEBACKQ" %%A IN (`BINREADWRITE.EXE "!BN_VGMPATH!" READ !BN_POINTER! 2`) DO (
		IF %%A EQU !BN_REG0! (
			SET /A "BN_CHREG_PULSE_DUTY=%%B >> 6"
			SET /A "BN_CHREG_PULSE_ENVMODE=(%%B >> 5) & 1"
			SET /A "BN_CHREG_PULSE_CV=(%%B >> 4) & 1"
			SET /A "BN_CHREG_PULSE_VOL=%%B & 0x0F"
			
		) ELSE IF %%A EQU !BN_REG2! (
			SET "BN_CHREG_PULSE_PL=%%B"
			
		) ELSE IF %%A EQU !BN_REG3! (
			SET /A "BN_CHREG_PULSE_PH=%%B & 0x07"
		
		)
	)
	SET /A "BN_POINTER+=2"

) ELSE IF !BN_COMMAND! EQU 97 (
	FOR /F "TOKENS=1,2 USEBACKQ" %%A IN (`BINREADWRITE.EXE "!BN_VGMPATH!" READ !BN_POINTER! 2`) DO SET /A "BN_RENDERSAMPLES=(%%A << 8) | %%B"
	SET /A "BN_POINTER+=2"

) ELSE IF !BN_COMMAND! EQU 98 (
	SET "BN_RENDERSAMPLES=735"
	
) ELSE IF !BN_COMMAND! EQU 99 (
	SET "BN_RENDERSAMPLES=882"
	
) ELSE IF !BN_COMMAND_HIGH_4_BITS! EQU 7 (
	SET /A "BN_RENDERSAMPLES=BN_COMMAND_LOW_4_BITS"

) ELSE (
	ECHO UNKNOWN COMMAND : !BN_COMMAND!
)

IF !BN_RENDERSAMPLES! NEQ 0 (
	SET /A "BN_CHSTAT_PULSE_PERIOD=(BN_CHREG_PULSE_PH << 8) | BN_CHREG_PULSE_PL"
	CALL :RENDER
)
GOTO A

:RENDER
FOR /L %%A IN (1, 1, !BN_RENDERSAMPLES!) DO (
	FOR /L %%B IN (1, 1, 20) DO (
		IF !BN_CHSTAT_PULSE_DUNTIL! LEQ !BN_CHSTAT_PULSE_PERIOD! (
			SET /A "BN_CHSTAT_PULSE_DUNTIL+=1"
		) ELSE (
			SET "BN_CHSTAT_PULSE_DUNTIL=1"
			SET /A "BN_CHSTAT_PULSE_PHASE+=1"
		)
		
		SET /A "TEMPVARI01=7 - (BN_CHSTAT_PULSE_PHASE & 0x07)"
		SET /A "TEMPVARI01=((BN_WAVE_P!BN_CHREG_PULSE_DUTY! >> TEMPVARI01) & 1) * BN_CHREG_PULSE_VOL * 3"
		
	)
	BINREADWRITE.EXE TEST.RAW WRITE DEC !TEMPVARI01!
)
GOTO :EOF