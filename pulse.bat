@ECHO OFF
SETLOCAL ENABLEDELAYEDEXPANSION

:: LICENCE TEXT ::

:: Copyright (C) 2025 Heemin

:: This program is free software; you can redistribute it and/or
:: modify it under the terms of the GNU General Public License
:: as published by the Free Software Foundation; either version 2
:: of the License, or (at your option) any later version.

:: This program is distributed in the hope that it will be useful,
:: but WITHOUT ANY WARRANTY; without even the implied warranty of
:: MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
:: GNU General Public License for more details.

:: You should have received a copy of the GNU General Public License
:: along with this program; if not, see <https://www.gnu.org/licenses/>.

:: ARGUMENTS ::
SET "BN_VGMPATH=%~1"
SET /A "BN_PREGOFFSET=(%~2 - 1) * 4"
SET /A "BN_REG0=BN_PREGOFFSET + 0"
REM SET /A "BN_REG1=BN_PREGOFFSET + 1"
SET /A "BN_REG2=BN_PREGOFFSET + 2"
SET /A "BN_REG3=BN_PREGOFFSET + 3"

SET "BN_POINTER=%~3"

SET "BN_AOUT_GAIN=4"
SET /A "BN_AOUT_OFFSET=128 - ((15 * BN_AOUT_GAIN) >> 1)"

:: MASK ROM TABLE ::
SET "BN_TABLE_WAVE_P0=128"
SET "BN_TABLE_WAVE_P1=192"
SET "BN_TABLE_WAVE_P2=240"
SET "BN_TABLE_WAVE_P3=252"
SET "BN_TABLE_LEN=0x0A0xFE0x140x020x280x040x500x060xA00x080x3C0x0A0x0E0x0C0x1A0x0E0x0C0x100x180x120x300x140x600x160xC00x180x480x1A0x100x1C0x200x1E"

:: CHANNEL STATE ::
SET "BN_CHSTAT_PULSE_PERIOD=0"
SET "BN_CHSTAT_PULSE_LEN_COUNTER=0"
SET "BN_CHSTAT_PULSE_PHASE=0"

:: CHANNEL REGISTERS ::
SET "BN_CHREG_PULSE_DUTY=0"
SET "BN_CHREG_PULSE_VOL=0"
SET "BN_CHREG_PULSE_LENHALT=1"
set "BN_CHREG_PULSE_CV=1"
SET "BN_CHREG_PULSE_PL=0"
SET "BN_CHREG_PULSE_PH=0"
SET "BN_CHREG_PULSE_LEN=0"
REM TODO : ENVELOPE, SWEEP UNITS

:: APU REGISTERS ::
SET "BN_APUREG_FCMODE=0"
SET "BN_APUREG_CHSTAT=0"

:: EMULATOR STATUS ::
SET "BN_EMU_RENDERSAMPLES=0"

REM TODO : DETECT JUMP / EOF POINT
:A
SET "BN_EMU_RENDERSAMPLES=0"
FOR /F "TOKENS=1,2,3 USEBACKQ" %%A IN (`BINREADWRITE.EXE "!BN_VGMPATH!" READ !BN_POINTER! 3`) DO ( 
	SET /A "BN_POINTER+=1"
	SET /A "BN_COMMAND_HIGH_4_BITS=%%A >> 4"
	SET /A "BN_COMMAND_LOW_4_BITS=%%A & 0x0F"

	IF %%A EQU 180 (
		SET /A "BN_POINTER+=2"
		IF %%B EQU !BN_REG0! (
			SET /A "BN_CHREG_PULSE_DUTY=%%C >> 6"
			SET /A "BN_CHREG_PULSE_LENHALT=(%%C >> 5) & 1"
			SET /A "BN_CHREG_PULSE_CV=(%%C >> 4) & 1"
			SET /A "BN_CHREG_PULSE_VOL=%%C & 0x0F"

		) ELSE IF %%B EQU !BN_REG2! (
			SET "BN_CHREG_PULSE_PL=%%C"

		) ELSE IF %%B EQU !BN_REG3! (
			SET /A "BN_CHREG_PULSE_PH=%%C & 0x07"
			SET /A "BN_CHREG_PULSE_LEN=%%C >> 3"

			SET /A "BN_CHSTAT_PULSE_LEN_COUNTER=BN_CHREG_PULSE_LEN << 2"
			SET /A "BN_CHSTAT_PULSE_LEN_COUNTER=!BN_TABLE_LEN:~%BN_CHSTAT_PULSE_LEN_COUNTER%,4! * 7417 + (BN_APUREG_FCMODE * 2304)"
			SET "BN_CHSTAT_PULSE_PHASE=0"
		
		) ELSE IF %%B EQU 21 (
			SET /A "BN_APUREG_CHENABLE=(%%C >> (%~2 - 1)) & 1"
		
		) ELSE IF %%B EQU 23 (
			SET /A "BN_APUREG_FCMODE=%%C >> 7"

		)

	) ELSE IF %%A EQU 97 (
		SET /A "BN_POINTER+=2"
		SET /A "BN_EMU_RENDERSAMPLES=(%%C << 8) | %%B"

	) ELSE IF %%A EQU 98 (
		SET "BN_EMU_RENDERSAMPLES=735"
		
	) ELSE IF %%A EQU 99 (
		SET "BN_EMU_RENDERSAMPLES=882"
		
	) ELSE IF !BN_COMMAND_HIGH_4_BITS! EQU 7 (
		SET /A "BN_EMU_RENDERSAMPLES=BN_COMMAND_LOW_4_BITS + 1"

	) ELSE (
		ECHO UNREGISTERED COMMAND : %%A

	)
)

IF !BN_EMU_RENDERSAMPLES! NEQ 0 CALL :RENDER !BN_EMU_RENDERSAMPLES!

GOTO A

:RENDER
FOR /L %%A IN (1, 1, %~1) DO (
	FOR /L %%B IN (1, 1, 20) DO (
		IF !BN_CHSTAT_PULSE_PERIOD! EQU 0 (
			SET /A "BN_CHSTAT_PULSE_PERIOD=(BN_CHREG_PULSE_PH << 8) | BN_CHREG_PULSE_PL"
			SET /A "BN_CHSTAT_PULSE_PHASE=(BN_CHSTAT_PULSE_PHASE + 1) & 0x07"

		) ELSE (
			SET /A "BN_CHSTAT_PULSE_PERIOD-=1"

		)

		IF !BN_CHSTAT_PULSE_LEN_COUNTER! NEQ 0 IF !BN_CHREG_PULSE_LENHALT! EQU 0 IF !BN_APUREG_CHENABLE! EQU 1 SET /A "BN_CHSTAT_PULSE_LEN_COUNTER-=1"
	)
	SET /A "BN_CHSTAT_PULSE_LEVEL=BN_CHREG_PULSE_VOL * (1 - ^!BN_CHSTAT_PULSE_LEN_COUNTER) * BN_APUREG_CHENABLE"
	SET /A "BN_AOUT_OUTPUT=BN_AOUT_OFFSET + ((BN_TABLE_WAVE_P!BN_CHREG_PULSE_DUTY! >> BN_CHSTAT_PULSE_PHASE) & 1) * BN_CHSTAT_PULSE_LEVEL * BN_AOUT_GAIN"
	BINREADWRITE.EXE TEST.RAW WRITE DEC !BN_AOUT_OUTPUT!
)
GOTO :EOF